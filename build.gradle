plugins {
    id("org.springframework.boot") version "3.5.3"
    id("io.spring.dependency-management") version "1.1.7"
    id 'java'
    id "com.diffplug.spotless" version "7.0.3"

    id("com.vanniktech.maven.publish") version "0.34.0"
}

group = 'io.github.mahdibohloul'
version = '2.0.0-RC2'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    implementation 'org.springframework:spring-context:6.2.8'
    implementation 'org.springframework:spring-core:6.2.8'
    implementation 'org.springframework.boot:spring-boot-starter-logging:3.5.3'

    implementation 'io.projectreactor:reactor-core:3.7.7'

    testImplementation 'org.aspectj:aspectjweaver'
    testImplementation 'org.mockito:mockito-core'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation("org.springframework.boot:spring-boot-starter-test")
}

mavenPublishing {
    publishToMavenCentral()
    signAllPublications()

    pom {
        name.set("projectreactor-retry-aop")
        description.set("ProjectReactor Retry AOP provides convenient retry support for your reactive applications.")
        url.set("https://github.com/mahdibohloul/projectreactor.retry.aop")
        licenses {
            license {
                name.set("MIT License")
                url.set("https://opensource.org/licenses/MIT")
                distribution.set("repo")
            }
        }
        developers {
            developer {
                id.set("mahdibohloul")
                name.set("Mahdi Bohloul")
                email.set("mahdiibohloul@gmail.com")
                url.set("https://github.com/mahdibohloul/")
            }
        }
        scm {
            url.set("https://github.com/mahdibohloul/projectreactor.retry.aop")
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        eclipse()
        trimTrailingWhitespace()
        leadingSpacesToTabs(4)
        endWithNewline()
    }
}
